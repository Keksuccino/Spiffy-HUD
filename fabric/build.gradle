
plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom' version '1.5-SNAPSHOT'
}

base {
    archivesName = "${mod_id}"
}

repositories {
    flatDir { dirs 'libs' }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")
    include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.3.2")))
    implementation 'com.github.Keksuccino:japng:0.5.3'
    modImplementation "maven.modrinth:load-my-resources:1.0.4-1.20.2-fabric"
    modImplementation "maven.modrinth:melody:1.0.1-1.20.1-1.20.4-fabric"
    modImplementation "maven.modrinth:konkrete:1.9.2-1.20.4-fabric"
    modImplementation "maven.modrinth:modmenu:9.0.0"
    //This will obviously break in-dev, but I just want to compile the frickin build rn and will fix this later
    implementation "flatdir:fancymenu:3.1.4"
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run_client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run_server")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
